class AetheriusStaticFunctions 
{
	static play bool CompareInventory(actor caller, name class1, name class2) 
	{
		return (caller.CountInv(class1) > caller.CountInv(class2));
	}
	
	static play int CompareInventoryPlayer1(actor caller, name class1)
	{
		return(caller.CountInv(class1));
	}
	
	static play void StartTensionCountDown()
	{
		int random1;
		int clearofmind = CallACS("MiscVarCheckDECORATE",10055);
		int clearofmindex = CallACS("MiscVarCheckDECORATE",10056);
		int misc1 = (clearofmind * 2) + (clearofmindex * 5) + 10;
		int misc2 = (clearofmind * 2) + (clearofmindex * 5) + 10;
		if (CallACS("MiscVarCheckDECORATE",181) > 0 && CallACS("MiscVarCheckDECORATE",184) == 0)
		{
			if (random(1,1000) > misc1) // 1% An attack will NOT start the countdown
			{
				CallACS("MiscVarSetDECORATE",184,1);
				random1 = random(1,misc2);
				if (random1 == 1) // 10% Chance for the timer to be cut by 25%
				{
					CallACS("MiscVarSetDECORATE",183,((CallACS("MiscVarCheckDECORATE",183) * 3) / 4));
				}
			}
		}
	}
}

// Menu Tooltips [borrowed from Nash's Tilt++ Mod for testing purposes. V:
class OptionMenuItemDVDSOption : OptionMenuItemOption
{
	String mTooltip;

	OptionMenuItemDVDSOption Init(String label, String tooltip, Name command, Name values, CVar graycheck = null, int center = 0)
	{
		mTooltip = tooltip;
		Super.Init(label, command, values, graycheck, center);
		return self;
	}
}

class OptionMenuItemDVDSTextField : OptionMenuItemTextField
{
	String mTooltip;

	OptionMenuItemDVDSTextField Init (String label, String tooltip, Name command, CVar graycheck = null)
	{
		mTooltip = tooltip;
		Super.Init(label, command, graycheck);
		return self;
	}
}

class OptionMenuItemDVDSSlider : OptionMenuItemSlider
{
	String mTooltip;

	OptionMenuItemDVDSSlider Init(String label, String tooltip, Name command, double min, double max, double step, int showval = 1)
	{
		mTooltip = tooltip;
		Super.Init(label, command, min, max, step, showval);
		return self;
	}
}

class OptionMenuItemDVDSSubmenu : OptionMenuItemSubmenu
{
	String mTooltip;

	OptionMenuItemDVDSSubmenu Init(String label, String tooltip, Name command, int param = 0, bool centered = false)
	{
		mTooltip = tooltip;
		Super.init(label, command, centered);
		mParam = param;
		return self;
	}
}

class DVDSMenu : OptionMenu
{
	override void Drawer ()
	{
		Super.Drawer();

		String tt;

		if (mDesc.mSelectedItem > -1)
		{
			if (mDesc.mItems[mDesc.mSelectedItem] is "OptionMenuItemDVDSSubmenu")
			{
				tt = StringTable.Localize(OptionMenuItemDVDSSubmenu(mDesc.mItems[mDesc.mSelectedItem]).mTooltip);
				if (dvdsdebug_menutooltipdisplays) Console.Printf("Submenu (selection# %d): '%s' (%d)", mDesc.mSelectedItem, tt, tt.Length());
			}

			if (mDesc.mItems[mDesc.mSelectedItem] is "OptionMenuItemDVDSOption")
			{
				tt = StringTable.Localize(OptionMenuItemDVDSOption(mDesc.mItems[mDesc.mSelectedItem]).mTooltip);
				if (dvdsdebug_menutooltipdisplays) Console.Printf("Option (selection# %d): '%s' (%d)", mDesc.mSelectedItem, tt, tt.Length());
			}

			if (mDesc.mItems[mDesc.mSelectedItem] is "OptionMenuItemDVDSSlider")
			{
				tt = StringTable.Localize(OptionMenuItemDVDSSlider(mDesc.mItems[mDesc.mSelectedItem]).mTooltip);
				if (dvdsdebug_menutooltipdisplays) Console.Printf("Slider (selection# %d): '%s' (%d)", mDesc.mSelectedItem, tt, tt.Length());
			}

			if (mDesc.mItems[mDesc.mSelectedItem] is "OptionMenuItemDVDSTextField")
			{
				tt = StringTable.Localize(OptionMenuItemDVDSTextField(mDesc.mItems[mDesc.mSelectedItem]).mTooltip);
				if (dvdsdebug_menutooltipdisplays) Console.Printf("TextField (selection# %d): '%s' (%d)", mDesc.mSelectedItem, tt, tt.Length());
			}
		}

		if (tt.Length() > 0)
		{
			font tipfont = OptionFont();
			let text = tt;
			int maxlength = (screen.GetWidth() / CleanXfac_1);
			BrokenLines lines = tipfont.BreakLines(text, maxlength);

			for (int i = 0; i < lines.Count(); ++i)
			{
				int fontheight = ((tipfont.GetHeight() * 0.875) * CleanYfac_1);
				int posx = (Screen.GetWidth() - tipfont.StringWidth(lines.StringAt(i)) * CleanXfac_1) / 2;
				int posy = ((fontheight * 2) + (i * fontheight));

				Screen.DrawText (tipfont, // font
					font.CR_GOLD, // OptionMenuSettings.mFontColorValue, // font color
					posx, // pos.x
					posy, // pos.y
					lines.StringAt(i), // string/text
					DTA_CleanNoMove_1, 
					true);
				
				if (dvdsdebug_menutooltipdisplays) Console.Printf("i: %d, count: %d, pos.x: %d, pos.y: %d, maxlength: %d", i, lines.Count(), posx, posy, maxlength);
			}
		}
	}
}
